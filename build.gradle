apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'maven-publish'
apply plugin: 'jetty'

group = 'net.praqma.roadshow.buep' // FIXME - add your initials or github id here
version = '0.0.34-SNAPSHOT'

System.properties.artifactoryUrlAndPort = System.properties.artifactoryUrlAndPort ?: "http://roadshowartifactory:8081/artifactory"
System.properties.tomcatUrlAndPort = System.properties.tomcatUrlAndPort ?: "http://roadshowtomcatserver:8080/"
System.properties.deployContext = System.properties.deployContext ?: "RC" // should be either RC or REL
System.properties.deployPath = System.properties.deployPath ?: String.format("RoadShow-%s", System.properties.deployContext) // should be set to RoadShow-yourinitial
System.properties.appName = System.properties.appName ?: String.format("/%s/Calculator", System.properties.deployPath)


task printProps << {
		println System.properties.artifactoryUrlAndPort
		println System.properties.tomcatUrlAndPort
		println System.properties.deployContext
		println System.properties.deployPath
		println System.properties.appName
}


description = """"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

def repositoryUrl(name) {
    System.properties.artifactoryUrlAndPort + "/libs-${name}-local"
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'2.26.0'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-server', version:'2.28.0'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
    compile group: 'junit', name: 'junit', version:'4.7'
}

jacoco {
    toolVersion = "0.7.1.201405082137"
}

test {
    filter {
        includeTestsMatching "net.praqma.roadshow.unit.*"
   }
}

stopPort = 4455
stopKey = 'stop'

task startApp(type: JettyRunWar) {
    daemon = true
}

task functionalTest(type: Test) {
    group 'verification'
    description 'Execute Selenium tests'

    dependsOn startApp
    filter {
        includeTestsMatching "net.praqma.roadshow.functional.*"
    }
    finalizedBy jettyStop
}

// Create test tasks to use on Jenkins so test results
// are seen as new, even though gradle is to smart to run
// the tasks again. The Jenkins JUnit plugin fails the build
// if test results are not new, eg. when building manually.
tasks.addRule("Pattern: jenkins<TaskName>: Update timestamp of test output files.") { String taskName ->
  if (taskName.startsWith('jenkins')) {
    String s = taskName - 'jenkins'
    Task testTask = tasks.findByName(s[0].toLowerCase() + s[1..-1])
    // If the rest of the taskName identifies a Test task, then create new task to update timestamp of test output file. This is needed for Jenkins
    if (testTask instanceof Test) {
      tasks.create(taskName) {
        dependsOn testTask
        doLast {
          def timestamp = System.currentTimeMillis()
          fileTree(dir: "${buildDir}/test-results/", include: "TEST*.xml").each {
            it.lastModified = timestamp 
          }
        }      
      }
    }
  }
}

checkstyle {
    ignoreFailures = true
}

findbugs {
    toolVersion = "3.0.1"
    ignoreFailures = true
}

publishing {
    repositories {
        maven {
            url repositoryUrl("snapshot")
        }
    }
    publications {
        roadShow(MavenPublication) {
            artifact war
        }
    }
}

configurations {
  // Configuration for artifacts deployed to Tomcat
  deployables
}

// display caching for snapshot version 
configurations.deployables.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

dependencies {
  deployables "${group}:${name}:${version}"
}

// TODO too many hardcoded values

task deploy(type: Copy) {
  description "Retrieve the war from repository and deploy to Tomcat"
  group "Web application"

  from (configurations.deployables) { rename { "roadshow.war" } }
  into '/tmp/tomcat/webapps'
}

task deployWar(type: Copy) {
  description "Deploy the war to Tomcat"
  group "Web application"

  from (war) { rename { "roadshow.war" } }
  into '/tmp/tomcat/webapps'
}
